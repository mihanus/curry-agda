-- Some theorems about operations on non-deterministic values

module nondet-thms where

open import bool
open import bool-thms
open import nat
open import eq
open import nat-thms
open import functions
open import nondet

----------------------------------------------------------------------
-- Theorems about values contained in non-deterministic values:

-- A proof that x is value of the non-deterministic tree y:
-- either it is equal to a deterministic value (ndrefl)
-- or it is somewhere in the tree.
-- If it is in the tree then we need to construct both branches of the tree,
-- and a proof that x is in one of the branches
-- A consequence of this is that any proof that x âˆˆ y contains the path
-- to x in the tree.
--
-- Example:
-- hInCoin : H âˆˆ coin
-- hInCoin = left (Val H) (Val T) ndrefl
--
-- Since H is on the left side of coin, we use the left constructor
-- The branches of the tree are (Val H) and (Val T),
-- and since H is identically equal to H this completes the proof.
data _âˆˆ_ {A : Set} (x : A) : (y : ND A) â†’ Set where
  ndrefl : x âˆˆ (Val x)
  left   : (l : ND A) â†’ (r : ND A) â†’ x âˆˆ l â†’ x âˆˆ (l ?? r)
  right  : (l : ND A) â†’ (r : ND A) â†’ x âˆˆ r â†’ x âˆˆ (l ?? r)

-- A basic inductive lemma that shows that âˆˆ is closed under function
-- application. That is, if x âˆˆ nx, then f x âˆˆ f $* nx 
--
-- Example:
-- ndCons : ... â†’ xs âˆˆ nxs â†’ (x :: xs) âˆˆ (_::_ x) $* nxs
âˆˆ-$* : {A B : Set} â†’ (f : A â†’ B) â†’ (x : A) â†’ (nx : ND A)
        â†’ x âˆˆ nx â†’ (f x) âˆˆ (f $* nx)
âˆˆ-$* f x (Val .x) ndrefl = ndrefl
âˆˆ-$* f x (l ?? r) (left  .l .r k) =
  left  (f $* l) (f $* r) (âˆˆ-$* f x l k)
âˆˆ-$* f x (l ?? r) (right .l .r k) =
  right (f $* l) (f $* r) (âˆˆ-$* f x r k)

-- This is a similar result as âˆˆ-$* but for non-deterministic application:
âˆˆ-*$* : {A B : Set} â†’ (x : A) â†’ (nx : ND A) â†’ (f : A â†’ B)
        â†’ (nf : A â†’ ND B) â†’ x âˆˆ nx â†’ f x âˆˆ nf x â†’ f x âˆˆ (nf *$* nx)
âˆˆ-*$* x (Val .x) f nf ndrefl pf = pf
âˆˆ-*$* x (l ?? r) f nf (left  .l .r p) pf =
  left (nf *$* l) (nf *$* r) (âˆˆ-*$* x l f nf p pf)
âˆˆ-*$* x (l ?? r) f nf (right .l .r p) pf = 
  right (nf *$* l) (nf *$* r) (âˆˆ-*$* x r f nf p pf)

----------------------------------------------------------------------
-- Theorems about '$*':

-- Combine two $* applications into one:
$*-$* : âˆ€ {A B C : Set} â†’ (f : B â†’ C) (g : A â†’ B) (xs : ND A)
            â†’ f $* (g $* xs) â‰¡ (f âˆ˜ g) $* xs
$*-$* f g (Val x) = refl
$*-$* f g (t1 ?? t2) rewrite $*-$* f g t1 | $*-$* f g t2 = refl

----------------------------------------------------------------------
-- Theorems about 'always':

-- Extend validity of a function with a deterministic argument to validity of
-- the corresponding non-deterministic function:
always-$* : âˆ€ {A : Set} â†’ (p : A â†’ ğ”¹) (xs : ND A)
          â†’ ((y : A) â†’ p y â‰¡ tt)
          â†’ always (p $* xs) â‰¡ tt
always-$* _ (Val y) prf = prf y
always-$* p (t1 ?? t2) prf
  rewrite always-$* p t1 prf
        | always-$* p t2 prf = refl

-- Extend validity of a function with a deterministic argument to validity of
-- the corresponding non-deterministic function:
always-*$* : âˆ€ {A : Set} â†’ (p : A â†’ ND ğ”¹) (xs : ND A)
               â†’ ((y : A) â†’ always (p y) â‰¡ tt)
               â†’ always (p *$* xs) â‰¡ tt
always-*$* _ (Val y) prf = prf y
always-*$* p (t1 ?? t2) prf
  rewrite always-*$* p t1 prf
        | always-*$* p t2 prf = refl

-- Extend validity of a deterministic function to validity of
-- corresponding function with non-deterministic result:
always-toND : âˆ€ {A : Set} â†’ (p : A â†’ ğ”¹) (x : A)
          â†’ (p x) â‰¡ tt â†’ always (toND p x) â‰¡ tt
always-toND _ _ p = p

----------------------------------------------------------------------
-- Theorems about 'satisfy':

-- A theorem like filter-map in functional programming:
satisfy-$* : âˆ€ {A B : Set} â†’ (f : A â†’ B) (xs : ND A) (p : B â†’ ğ”¹)
             â†’ (f $* xs) satisfy p â‰¡ xs satisfy (p âˆ˜ f)
satisfy-$* _ (Val x) _ = refl
satisfy-$* f (t1 ?? t2) p
 rewrite satisfy-$* f t1 p
       | satisfy-$* f t2 p = refl

-- Extend validity of function with deterministic argument to validity of
-- non-deterministic function:
satisfy-*$* : âˆ€ {A B : Set} â†’ (p : B â†’ ğ”¹) (f : A â†’ ND B) (xs : ND A)
               â†’ ((y : A) â†’ (f y) satisfy p â‰¡ tt)
               â†’ (f *$* xs) satisfy p â‰¡ tt
satisfy-*$* _ _ (Val y) prf = prf y
satisfy-*$* p f (t1 ?? t2) prf
  rewrite satisfy-*$* p f t1 prf
        | satisfy-*$* p f t2 prf = refl

----------------------------------------------------------------------
-- Theorems about 'every':

every-$* : âˆ€ (f : â„• â†’ â„•) (v : â„•) (x : ND â„•) â†’
         every _=â„•_ v x â‰¡ tt â†’ every _=â„•_ (f v) (f $* x) â‰¡ tt
every-$* f v (Val x) u rewrite =â„•-to-â‰¡ {v} {x} u | =â„•-refl (f x) = refl
every-$* f v (t1 ?? t2) u
  rewrite every-$* f v t1 (&&-fst u)
        | every-$* f v t2 (&&-snd {every _=â„•_ v t1} {every _=â„•_ v t2} u) = refl

----------------------------------------------------------------------
-- This theorms allows to weaken a predicate which is always satisfied:
weak-always-predicate : âˆ€ {A : Set} â†’ (p p1 : A â†’ ğ”¹) (xs : ND A)
                â†’ xs satisfy p â‰¡ tt
                â†’ xs satisfy (Î» x â†’ p1 x || p x) â‰¡ tt
weak-always-predicate p p1 (Val x) u rewrite u | ||-tt (p1 x) = refl
weak-always-predicate p p1 (t1 ?? t2) u
  rewrite weak-always-predicate p p1 t1 (&&-fst u)
        | weak-always-predicate p p1 t2 (&&-snd {t1 satisfy p} {t2 satisfy p} u)
  = refl

----------------------------------------------------------------------
